gw = glob_wildcards('models/{DATASET}/fold-{FOLD}/negative-2/MultiRBP.h5')

rule ALL:
    input:
        expand('processed/{DATASET}/fold-{FOLD}/negative-2/results.csv', zip, DATASET=gw.DATASET, FOLD=gw.FOLD),
        'processed/results.MultiRBP.csv'

rule predict:
    input:
        fasta = 'inputs/{DATASET}/fold-{FOLD}/input.fasta',
        model = 'models/{DATASET}/fold-{FOLD}/negative-2/MultiRBP.h5'
    output:
        results = 'processed/{DATASET}/fold-{FOLD}/negative-2/results.tmp.csv',
    shell:
        './predict.MultiRBP.sbatch.sh {input.fasta} {input.model} {output.results}'

        # """
        # set +u
        # source $HOME/.bashrc
        # conda activate multirbp-cpu
        # set -u

        # python -u scripts/eval.py --test-input {input.fasta} --model-file {input.model} --output-csv {output.results}
        # """

rule add_sample_type_col:
    input:
        results = 'processed/{DATASET}/fold-{FOLD}/negative-2/results.tmp.csv',
        id_rbps_dict = 'inputs/{DATASET}/fold-{FOLD}/id_rbps_dict.json'
    output:
        results = 'processed/{DATASET}/fold-{FOLD}/negative-2/results.csv'
    run:
        import json

        with open(input.id_rbps_dict) as json_f:
            id_rbps_dict = json.load(json_f)
        # Format is method,dataset,name,fold#,model_type,sample_id,score(,sample_type)
        with open(output.results, "w") as f_out:
            with open(input.results) as f_in:
                for line in f_in:
                    name = line.strip().split(',')[2]
                    sample_id = line.strip().split(',')[5]
                    # Check if name in sample_id according to json_dict (truth)
                    if name in id_rbps_dict[sample_id]: # Bound!
                        sample_type = 'positive'
                    else:
                        sample_type = 'negative-2'
                    
                    print(','.join([line.strip(), sample_type]), file=f_out)


rule aggregate_results:
    input:
        expand('processed/{DATASET}/fold-{FOLD}/negative-2/results.csv', zip, DATASET=gw.DATASET, FOLD=gw.FOLD)
    output:
        'processed/results.MultiRBP.csv'
    shell:
        'cat {input} > {output}'